1   Program  -> ProgramHead DeclarePart ProgramBody 
2   ProgramHead -> PROGRAM ProgramName 
3   ProgramName -> ID 
4   DeclarePart -> TypeDec VarDec ProcDec 
5   TypeDec  -> LAMBDA 
6   TypeDec  -> TypeDeclaration 
7   TypeDec  -> TYPE TypeDecList 
8   TypeDecList -> TypeId EQ TypeName SEMI TypeDecMore 
9   TypeDecMore -> LAMBDA 
10  TypeDecMore -> TypeDecList 
11  TypeId   -> ID 
12  TypeName -> BaseType 
13  TypeName -> StructureType 
14  TypeName -> ID 
15  BaseType -> INTEGER 
16  BaseType -> CHAR 
17  StructureType -> ArrayType 
18  StructureType -> RecType 
19  ArrayType -> ARRAY LMIDPAREN Low UNDERANGE Top RMIDPAREN OF BaseType 
20  Low      -> INTC 
21  Top      -> INTC 
22  RecType  -> RECORD FieldDecList END 
23  FieldDecList -> BaseType IdList SEMI FieldDecMore 
24  FieldDecList -> ArrayType IdList SEMI FieldDecMore 
25  FieldDecMore -> LAMBDA 
26  FieldDecMore -> FieldDecList 
27  IdList   -> ID IdMore 
28  IdMore   -> LAMBDA 
29  IdMore   -> COMMA IdList 
30  VarDec   -> LAMBDA 
31  VarDec   -> VarDeclaration 
32  VarDeclaration -> VAR VarDecList 
33  VarDecList -> TypeName VarIdList SEMI VarDecMore 
34  VarDecMore -> LAMBDA 
35  VarDecMore -> VarDecList 
36  VarIdList -> ID VarIdMore 
37  VarIdMore -> LAMBDA 
38  VarIdMore -> COMMA VarIdList 
39  ProcDec  -> LAMBDA 
40  ProcDec  -> ProcDeclaration 
41  ProcDeclaration -> PROCEDURE ProcName LPAREN ParamList RPAREN SEMI ProcDecPart ProcBody ProcDecMore 
42  ProcDecMore -> LAMBDA 
43  ProcDecMore -> ProcDeclaration 
44  ProcName -> ID 
45  ParamList -> LAMBDA 
46  ParamList -> ParamDecList 
47  ParamDecList -> Param ParamMore 
48  ParamMore -> LAMBDA 
49  ParamMore -> SEMI ParamDecList 
50  Param    -> TypeName FormList 
51  Param    -> VAR TypeName FormList 
52  FormList -> ID FidMore 
53  FidMore  -> LAMBDA 
54  FidMore  -> COMMA FormList 
55  ProcDecPart -> DeclarePart 
56  ProcBody -> ProgramBody 
57  ProgramBody -> BEGIN StmList END 
58  StmList  -> Stm StmMore 
59  StmMore  -> LAMBDA 
60  StmMore  -> SEMI StmList 
61  Stm      -> ConditionalStm 
62  Stm      -> LoopStm 
63  Stm      -> InputStm 
64  Stm      -> OutputStm 
65  Stm      -> ReturnStm 
66  Stm      -> ID AssCall 
67  AssCall  -> AssignmentRest 
68  AssCall  -> CallStmRest 
69  AssignmentRest -> VariMore ASSIGN Exp 
70  ConditionalStm -> IF RelExp THEN StmList ELSE StmList FI 
71  LoopStm  -> WHILE RelExp DO StmList ENDWH 
72  InputStm -> READ LPAREN InVar RPAREN 
73  InVar    -> ID 
74  OutputStm -> WRITE LPAREN Exp RPAREN 
75  ReturnStm -> RETURN 
76  CallStmRest -> LPAREN ActParamList RPAREN 
77  ActParamList -> LAMBDA 
78  ActParamList -> Exp ActParamMore 
79  ActParamMore -> LAMBDA 
80  ActParamMore -> COMMA ActParamList 
81  RelExp   -> Exp OtherRelE 
82  OtherRelE -> CmpOp Exp 
83  Exp      -> Term OtherTerm 
84  OtherTerm -> LAMBDA 
85  OtherTerm -> AddOp Exp 
86  Term     -> Factor OtherFactor 
87  OtherFactor -> LAMBDA 
88  OtherFactor -> MultOp Term 
89  Factor   -> LPAREN Exp RPAREN 
90  Factor   -> INTC 
91  Factor   -> Variable 
92  Variable -> ID VariMore 
93  VariMore -> LAMBDA 
94  VariMore -> LMIDPAREN Exp RMIDPAREN 
95  VariMore -> DOT FieldVar 
96  FieldVar -> ID FieldVarMore 
97  FieldVarMore -> LAMBDA 
98  FieldVarMore -> LMIDPAREN Exp RMIDPAREN 
99  CmpOp    -> LT 
100 CmpOp    -> EQ 
101 AddOp    -> PLUS 
102 AddOp    -> MINUS 
103 MultOp   -> TIMES 
104 MultOp   -> OVER 
